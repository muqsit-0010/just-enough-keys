//file:noinspection GroovyAssignabilityCheck
plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

architectury {
    platformSetupLoomIde()
    fabric()
}
repositories {
    maven {
        url = 'https://maven.minecraftforge.net/'
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    include "curse.maven:cloth-config-348521:4633442"
    //include "curse.maven:dont-drop-it-390383:4026541"
    //include "curse.maven:interactic-510974:4576411"

    implementation(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    developmentFabric(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}
processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("fabric")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            name = 'myRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }
}

if (System.getenv('CURSE_DEPLOY')) {
    curseforge {
        apiKey = System.getenv('CURSE_DEPLOY')
        //noinspection GroovyAssignabilityCheck
        project {
            id = project.curseforge_id
            releaseType = "${release_type}"
            addGameVersion "Fabric"
            addGameVersion "${minecraft_version}"
            addGameVersion "${java_version}"
            addGameVersion "1.18"
            addGameVersion "Java 18"
            mainArtifact(remapJar){

                displayName = "${archivesBaseName}-fabric-${minecraft_version}-${mod_version}"
            }
            changelog = new File("${rootDir.toPath()}/changelog.md").text
            changelogType = 'markdown'
            relations {
                requiredDependency 'fabric-api'
            }
        }
    }
}