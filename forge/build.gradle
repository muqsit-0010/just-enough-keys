//file:noinspection GroovyAssignabilityCheck
plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfigs = ["justenoughkeys.mixins.json","justenoughkeys.mixin.forge.json"]
    }
}

repositories {

    maven {
        url "https://www.cursemaven.com"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    //include "curse.maven:jei-238222:4712868"
    //include "curse.maven:sophisticated-backpacks-422301:4808060"
    //include "curse.maven:journeymap-32274:4774257"

    implementation(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    developmentForge(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")

}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("forge")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            name = 'myRepo'
            url = layout.buildDirectory.dir('repo')
        }
    }
}

if (System.getenv('CURSE_DEPLOY')) {
    curseforge {
        apiKey = System.getenv('CURSE_DEPLOY')
        //noinspection GroovyAssignabilityCheck
        project {
            id = project.curseforge_id
            releaseType = "${release_type}"
            addGameVersion "Forge"
            addGameVersion "${minecraft_version}"
            addGameVersion "1.18"
            addGameVersion "Java 18"
            addGameVersion "${java_version}"
            mainArtifact(remapJar){
                displayName = "${archivesBaseName}-forge-${minecraft_version}-${mod_version}"
            }
            changelog = new File("${rootDir.toPath()}/changelog.md").text
            changelogType = 'markdown'
        }
    }
}